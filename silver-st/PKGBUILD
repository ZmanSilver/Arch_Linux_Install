# Maintainer: Zachary Silver <zmansilver@gmail.com>

pkgname=silver-st
pkgver=0.8.1
pkgrel=2
pkgdesc='A customized clone of a simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc' 'ttf-dejavu')
makedepends=('ncurses')
provides=($pkgname)
conflicts=($pkgname 'st')
url="www.github.com/ZmanSilver/silver-st"
source=(https://www.dropbox.com/s/utj3u7b4p9zxv3e/silver-st.tar.gz
	config.h)
sha256sums=('f93437c9488eae9104f7018d9ec9d880c2ca7a77a0a72c21d605a06452a50f02'
            '7f32945da52ede8ea6ae5b1e3222f743529ff7076136e82583d40aec925e2c99')

prepare() {
  cd $srcdir/$pkgname
  # skip terminfo which conflicts with ncurses
  sed -i '/tic /d' Makefile
  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
